subrepo = subrepo_name()
subrepo = f"///{subrepo}" if subrepo else ""

CONFIG.setdefault("HELM_TOOL", subrepo + "//tools/kubernetes:helm")

def helm_chart(name:str, chart_yaml:str="Chart.yaml", values_file:str="values.yaml",
               templates:str=None, tests:str=None, crds:str=None, helmignore:str=".helmignore", srcs:list=[],
               labels:list=[], visibility:list=None, test_only:bool&testonly=False):
    """Source files for a Helm chart.

    Args:
      name (str): Name of the rule.
      chart_yaml (str): Chart.yaml file of the chart.
      values_file (str): values.yaml file of the chart.
      templates (str): Template files of the chart.
      tests (str): Test files of the chart.
      crds (str): Custom Resource Definitions (CRDs) of the chart.
      helmignore (str): Helmignore file of the chart.
      srcs (str): Additional source files of the chart (eg. custom config files).
      labels (list): Labels for this rule.
      visibility (list): Visibility declaration of the rule.
      test_only (bool): If true the rule is only visible to test targets.
    """

    assert chart_yaml, "Chart.yaml is required"
    assert values_file, "values.yaml is required"

    templates = templates or filegroup(
        name = name,
        tag = "templates",
        srcs = glob(["templates/**"], exclude = ["templates/tests", "templates/tests/**"]),
    )

    tests = tests or filegroup(
        name = name,
        tag = "tests",
        srcs = glob(["templates/tests/**"]),
    )

    crds = crds or filegroup(
        name = name,
        tag = "crds",
        srcs = glob(["crds/**"]),
    )

    srcs = [
        chart_yaml,
        values_file,
        templates,
        tests,
        crds,
    ] + srcs

    if helmignore:
        srcs += [helmignore]

    return filegroup(
        name = name,
        srcs = srcs,
        labels = labels,
        visibility = visibility,
        test_only = test_only,
    )

CONFIG.setdefault("HELM_DOCS_TOOL", subrepo + "//tools/kubernetes:helm-docs")

# TODO: add support for ignore-file
def helm_docs(name:str, chart_yaml:str="Chart.yaml", values_file:str="values.yaml", out:str="README.md",
              templates:list=None, sort_values_order:str="alphanum",
              labels:list=[], visibility:list=None, test_only:bool&testonly=False):
    """Generate a readme file from values and chart info.

    Args:
      name (str): Name of the rule.
      chart_yaml (str): Chart.yaml file of the chart.
      values_file (str): values.yaml file of the chart.
      out (str): Output name of the generated file.
      templates (list): Template files of the chart.
      sort_values_order (str): Order in which to sort the values table.
                               ("alphanum" or "file") (default "alphanum")
      labels (list): Labels for this rule.
      visibility (list): Visibility declaration of the rule.
      test_only (bool): If true the rule is only visible to test targets.
    """

    assert chart_yaml, "Chart.yaml is required"
    assert values_file, "values.yaml is required"
    assert sort_values_order == "alphanum" or sort_values_order == "file", "Valid values for sort_values_order are alphanum and file"

    return genrule(
        name = name,
        srcs = {
            "chart": [chart_yaml, values_file],
            "templates": templates,
        },
        cmd = [
            " ".join([
                "$TOOLS_HELM_DOCS",
                "--chart-search-root", '"$PKG"',
                "--output-file", "_out",
                "--sort-values-order", sort_values_order,
                #"$(echo ${SRCS_TEMPLATES-} | awk -v pwd=$(pwd) 'BEGIN { RS=\" \" }  { print \"--template-files \" pwd\"/\"$1 }')",
                '$(for t in ${SRCS_TEMPLATES-}; do echo --template-files "$(pwd)"/$t; done)'
            ]),
            'mv "$PKG"/_out "$OUT"',
        ],
        outs = [out],
        tools = {"helm_docs": [CONFIG.HELM_DOCS_TOOL]},
        labels = labels,
        visibility = visibility,
        test_only = test_only,
    )

def helm_lint(name:str, chart:str, labels:list=[], visibility:list=None):
    """Lints a Helm chart.

    Args:
      name (str): Name of the rule.
      labels (list): Labels for this rule.
      visibility (list): Visibility declaration of the rule.
    """

    return gentest(
        name = name,
        data = {
            "chart": [chart],
        },
        test_cmd = f'"$TOOLS_HELM" lint "$PKG"',
        test_tools = {"helm": [CONFIG.HELM_TOOL]},
        labels = labels,
        visibility = visibility,
        no_test_output = True,
    )

# TODO: add version option
# TODO: add app_version option
def helm_package(name:str, chart:str, labels:list=[], visibility:list=None, test_only:bool&testonly=False):
    """Packages a Helm chart into a chart archive.

    Args:
      name (str): Name of the rule.
      chart (str): Helm chart package.
      labels (list): Labels for this rule.
      visibility (list): Visibility declaration of the rule.
      test_only (bool): If true the rule is only visible to test targets.
    """

    assert chart, "Chart is required"

    return genrule(
        name = name,
        srcs = [chart],
        cmd = '"$TOOLS_HELM" package --destination _helm-out "$PKG"',
        output_dirs = ["_helm-out"],
        tools = {"helm": [CONFIG.HELM_TOOL]},
        labels = labels,
        visibility = visibility,
        test_only = test_only,
    )

# TODO: add timeout option
def helm_e2e_test(name:str, chart:str, release_name:str=None, namespace:str=None,
                  kubeconfig:str=None, kube_context:str=None, kube_token:str=None, kube_apiserver:str=None,
                  atomic:bool=True, run_tests:bool=False, debug:bool=False,
                  flaky:bool|int=0, timeout:int=0,
                  labels:list=[], visibility:list=None):
    """Installs a Helm chart on a Kubernetes cluster to test its functionality.

    Args:
      name (str): Name of the rule.
      chart (str): Helm chart package to test.
      release_name (str): Release name for the installed chart. Chosen automatically if not given.
      namespace (str): Kubernetes namespace where the chart will be installed. The namespace must already exist.
      kubeconfig (str): Kubeconfig file.
      kube_context (str): Name of the kubeconfig context to use.
      kube_token (str): Bearer token used for authentication against the API server.
      kube_apiserver (str): Kubernetes API server address.
      atomic (bool): Delete the installation on failure.
      run_tests (bool): Run tests defined in the chart. (Otherwise it just installs and uninstalls the chart)
      debug (bool): Enable verbose output.
      flaky (bool | int): If true the test will be marked as flaky and automatically retried.
      timeout (int): Length of time in seconds to allow the test to run for before killing it.
      labels (list): Labels for this rule.
      visibility (list): Visibility declaration of the rule.
    """

    data = {
        "chart": [chart],
    }

    helm_cmd = '"$TOOLS_HELM"'

    if namespace:
        helm_cmd += f' --namespace "{namespace}"'

    if kubeconfig:
        data["kubeconfig"] = [kubeconfig]

        helm_cmd += ' --kubeconfig "$DATA_KUBECONFIG"'

    if kube_context:
        helm_cmd += f' --kube-context "{kube_context}"'

    if kube_token:
        helm_cmd += f' --kube-token "{kube_token}"'

    if kube_apiserver:
        helm_cmd += f' --kube-apiserver "{kube_apiserver}"'

    if debug:
        helm_cmd += " --debug"

    release_name = release_name or name

    install_cmd = f"{helm_cmd} install --wait"

    if atomic:
        install_cmd += " --atomic"

    if timeout > 0:
        install_cmd += f" --timeout {timeout}"

    install_cmd += f' {release_name} "$DATA_CHART"'

    cmd = [install_cmd]

    if run_tests:
        test_cmd = f"{helm_cmd} test --logs"

        if timeout > 0:
            test_cmd += f" --timeout {timeout}"

        test_cmd += f" {release_name}"

        cmd += [test_cmd]

    return gentest(
        name = name,
        data = data,
        test_cmd = f"trap '{helm_cmd} uninstall {release_name}' EXIT;" + " && ".join(cmd),
        test_tools = {"helm": [CONFIG.HELM_TOOL]},
        pass_env = ["KUBECONFIG"],
        flaky = flaky,
        timeout = timeout,
        labels = labels,
        visibility = visibility,
        no_test_output = True,
    )
