def kubectl_binary(name:str, version:str, hashes:list=None, out:str="", labels:list=[],
                   visibility:list=None, test_only:bool&testonly=False, host:bool=True):
    """Downloads a precompiled kubectl binary.

    Args:
      name (str): Name of the rule.
      version (str): Version of the binary to download.
      hashes (list): Hashes to verify the download against.
      out (str): Output name of the binary. Chosen automatically if not given.
      labels (list): Labels for this rule.
      visibility (list): Visibility declaration of the rule.
      test_only (bool): If true the rule is only visible to test targets.
      host (bool): If true the rule will always target the host OS and Arch.
    """

    os = CONFIG.HOSTOS if host else CONFIG.OS
    arch = CONFIG.HOSTARCH if host else CONFIG.ARCH

    return remote_file(
        name = name,
        url = f"https://storage.googleapis.com/kubernetes-release/release/v{version}/bin/{os}/{arch}/kubectl",
        hashes = hashes,
        out = out or "kubectl",
        binary = True,
        labels = labels,
        visibility = visibility,
        test_only = test_only,
    )

def kind_binary(name:str, version:str, hashes:list=None, out:str="", labels:list=[],
                visibility:list=None, test_only:bool&testonly=False, host:bool=True):
    """Downloads a precompiled kind binary.

    Args:
      name (str): Name of the rule.
      version (str): Version of the binary to download.
      hashes (list): Hashes to verify the download against.
      out (str): Output name of the binary. Chosen automatically if not given.
      labels (list): Labels for this rule.
      visibility (list): Visibility declaration of the rule.
      test_only (bool): If true the rule is only visible to test targets.
      host (bool): If true the rule will always target the host OS and Arch.
    """

    os = CONFIG.HOSTOS if host else CONFIG.OS
    arch = CONFIG.HOSTARCH if host else CONFIG.ARCH

    return remote_file(
        name = name,
        url = f"https://github.com/kubernetes-sigs/kind/releases/download/v{version}/kind-{os}-{arch}",
        hashes = hashes,
        out = out or "kind",
        binary = True,
        labels = labels,
        visibility = visibility,
        test_only = test_only,
    )

def helm_binary(name:str, version:str, hashes:list=None, out:str="", labels:list=[],
                visibility:list=None, test_only:bool&testonly=False, host:bool=True):
    """Downloads a precompiled helm binary.

    Args:
      name (str): Name of the rule.
      version (str): Version of the binary to download.
      hashes (list): Hashes to verify the download against.
      out (str): Output name of the binary. Chosen automatically if not given.
      labels (list): Labels for this rule.
      visibility (list): Visibility declaration of the rule.
      test_only (bool): If true the rule is only visible to test targets.
      host (bool): If true the rule will always target the host OS and Arch.
    """

    os = CONFIG.HOSTOS if host else CONFIG.OS
    arch = CONFIG.HOSTARCH if host else CONFIG.ARCH

    return genrule(
        name = name,
        srcs = [remote_file(
            name = name,
            _tag = "download",
            url = f"https://get.helm.sh/helm-v{version}-{os}-{arch}.tar.gz",
            hashes = hashes,
            out = f"helm-{version}-{os}-{arch}.tar.gz",
            labels = labels,
        )],
        outs = [out or "helm"],
        binary = True,
        cmd = '"$TOOL" x "$SRCS" helm -o tmp && mv $(find tmp -name "helm") "$OUT"',
        tools = [CONFIG.JARCAT_TOOL],
        labels = labels,
        visibility = visibility,
        test_only = test_only,
    )
