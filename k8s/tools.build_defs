def helm_binary(name:str, version:str=None, hashes:list=None, out:str="",
                         visibility:list=None, test_only:bool&testonly=False, host:bool=True):
    """Downloads a precompiled helm binary.

    Args:
      name (str): Name of the rule.
      version (str): Version of the binary to download.
      hashes (list): Hashes to verify the download against.
      out (str): Output name of the binary. Chosen automatically if not given.
      visibility (list): Visibility declaration of the rule.
      test_only (bool): If true the rule is only visible to test targets.
      host (bool): If true the rule will always target the host OS and Arch.
    """

    os = CONFIG.HOSTOS if host else CONFIG.OS
    arch = CONFIG.HOSTARCH if host else CONFIG.ARCH

    return genrule(
        name = name,
        srcs = [remote_file(
            name = name,
            _tag = "download",
            url = f"https://get.helm.sh/helm-v{version}-{os}-{arch}.tar.gz",
            hashes = hashes,
            out = f"helm-{version}-{os}-{arch}.tar.gz",
        )],
        outs = [out or "helm"],
        binary = True,
        cmd = '"$TOOL" x "$SRCS" helm -o tmp && mv $(find tmp -name "helm") "$OUT"',
        tools = [CONFIG.JARCAT_TOOL],
        visibility = visibility,
        test_only = test_only,
    )
