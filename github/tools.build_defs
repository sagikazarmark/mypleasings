def hub_binary(name:str, version:str, hashes:list=None, out:str="", labels:list=[],
               visibility:list=None, test_only:bool&testonly=False, host:bool=True):
    """Downloads a precompiled hub binary.

    Args:
      name (str): Name of the rule.
      version (str): Version of the binary to download.
      hashes (list): Hashes to verify the download against.
      out (str): Output name of the binary. Chosen automatically if not given.
      labels (list): Labels for this rule.
      visibility (list): Visibility declaration of the rule.
      test_only (bool): If true the rule is only visible to test targets.
      host (bool): If true the rule will always target the host OS and Arch.
    """

    os = CONFIG.HOSTOS if host else CONFIG.OS
    arch = CONFIG.HOSTARCH if host else CONFIG.ARCH

    return remote_file(
        name = name,
        url = f"https://github.com/github/hub/releases/download/v{version}/hub-{os}-{arch}-{version}.tgz",
        hashes = hashes,
        out = out,
        extract = True,
        exported_files = [f"hub-{os}-{arch}-{version}/bin/hub"],
        binary = True,
        labels = labels,
        visibility = visibility,
        test_only = test_only,
    )

def gh_binary(name:str, version:str, hashes:list=None, out:str="", labels:list=[],
              visibility:list=None, test_only:bool&testonly=False, host:bool=True):
    """Downloads a precompiled GitHub CLI binary.

    Args:
      name (str): Name of the rule.
      version (str): Version of the binary to download.
      hashes (list): Hashes to verify the download against.
      out (str): Output name of the binary. Chosen automatically if not given.
      labels (list): Labels for this rule.
      visibility (list): Visibility declaration of the rule.
      test_only (bool): If true the rule is only visible to test targets.
      host (bool): If true the rule will always target the host OS and Arch.
    """

    os = CONFIG.HOSTOS if host else CONFIG.OS
    arch = CONFIG.HOSTARCH if host else CONFIG.ARCH

    download_os = {
        "darwin": "macOS",
    }.get(os, os)

    return remote_file(
        name = name,
        url = f"https://github.com/cli/cli/releases/download/v{version}/gh_{version}_{download_os}_{arch}.tar.gz",
        hashes = hashes,
        out = out,
        extract = True,
        exported_files = [f"gh_{version}_{download_os}_{arch}/bin/gh"],
        binary = True,
        labels = labels,
        visibility = visibility,
        test_only = test_only,
    )
