CONFIG.setdefault("OPENAPI_GENERATOR_IMAGE", "openapitools/openapi-generator-cli")
CONFIG.setdefault("OPENAPI_GENERATOR_VERSION", "v4.3.1")

# TODO: add support for config file
def openapi_library(name:str, spec:str, generator:str, out:str=None, additional_properties:dict={}, deps:list=[], visibility:list=None, test_only:bool&testonly=False, strip:list=None):
    """Generates client or server libraries and stubs from an OpenAPI specification.

    It uses the unofficial OpenAPI Generator under the hood: https://github.com/OpenAPITools/openapi-generator

    Args:
      name (str): Name of the rule.
      spec (str): OpenAPI specification.
      generator (str): Generator to use. See the documentation for the list of generators.
      out (str): Name of the output directory
      additional_properties (dict): A list of additional properties passed to the generator.
                                    See the documentation for the list of available options for each generator.
      deps (list): Dependencies
      visibility (list): Visibility specification
      test_only (bool): If true this rule will only be visible to tests.
      strip (list): List of paths to strip from the generated target.
    """

    out = out or f"openapi/{name}"

    main_cmd = f'docker run --rm -v "$TMP_DIR/$SRC:/build/$SRC" -v "$OUT:$OUT" {CONFIG.OPENAPI_GENERATOR_IMAGE}:{CONFIG.OPENAPI_GENERATOR_VERSION} generate -i "/build/$SRC" -o "$OUT" -g {generator}'

    for key, value in additional_properties.items():
        main_cmd += f" --additional-properties {key}={value}"

    if CONFIG.HOSTOS == "linux": # We need to change ownership on linux, because the container runs as root
        main_cmd += ' && sudo chown -R $(id -u).$(id -g) "$OUT"'

    cmd = [main_cmd]

    if strip:
        cmd += [f"rm -rf $OUT/{s}" for s in strip]

    return build_rule(
        name = name,
        srcs = [spec],
        deps = deps,
        outs = [out],
        cmd = " && ".join(cmd),
        building_description = "Generating...",
        sandbox = False,
        visibility = visibility,
        test_only = test_only,
    )
